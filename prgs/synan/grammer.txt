Source -> Decls .

Decls -> Decl DeclsRest .

Decl -> typ identifier : Type ; .
Decl -> var identifier : Type ; .
Decl -> fun identifier ( ParDeclsEps ) : Type BodyEps ; .

DeclsRest -> .
DeclsRest -> Decls.

ParDeclsEps -> .
ParDeclsEps -> ParDecls .

ParDecls -> ParDecl ParDeclsRest .

ParDecl -> identifier : Type .

ParDeclsRest -> .
ParDeclsRest -> , ParDecls .

BodyEps -> .
BodyEps -> = DisjExpr .

Type -> void | int | char | bool .
Type -> arr [ DisjExpr ] Type | ptr Type .
Type -> identifier | ( Type ).
Type -> rec ( CompDecls ) .

CompDecls -> CompDecl CompDeclsRest.

CompDecl -> identifier : Type .

CompDeclsRest -> .
CompDeclsRest -> , CompDecls .

DisjExpr -> ConjExpr DisjExprRest .

DisjExprRest -> .
DisjExprRest -> \ ConjExpr DisjExprRest | ^ ConjExpr DisjExprRest .

ConjExpr -> RelExpr ConjExprRest .

ConjExprRest -> .
ConjExprRest -> & RelExpr ConjExprRest.

RelExpr -> AddExpr RelExprRest.

RelExprRest -> .
RelExprRest -> eql AddExpr | neq AddExpr | lte AddExpr .
RelExprRest -> mte AddExpr | < AddExpr | > AddExpr .

AddExpr -> MulExpr AddExprRest.

AddExprRest -> .
AddExprRest -> + MulExpr AddExprRest | - MulExpr AddExprRest.

MulExpr -> PrefExpr MulExprRest.

MulExprRest -> .
MulExprRest -> * PrefExpr MulExprRest | / PrefExpr MulExprRest | % PrefExpr MulExprRest .

PrefExpr -> PstfExpr | ! PrefExpr | + PrefExpr | - PrefExpr .
PrefExpr -> @ PrefExpr | $ PrefExpr | new ( Type ) | del ( DisjExpr ) .

PstfExpr -> Expr PstfExprRest .

PstfExprRest -> .
PstfExprRest -> [ DisjExpr ] PstfExprRest .
PstfExprRest -> ~ identifier PstfExprRest .

Expr -> AtomExpr | ( DisjExpr CastEps ) | { Stmts : DisjExpr WhereEps } .

AtomExpr -> literal | identifier CallEps .

CallEps -> .
CallEps -> ( ArgsEps ) .

ArgsEps -> .
ArgsEps -> Args ArgsRest .

Args -> DisjExpr .

ArgsRest -> , Args ArgsRest .
ArgsRest -> .

CastEps -> .
CastEps -> : Type .

WhereEps -> where Decls .
WhereEps -> .

Stmts -> Stmt StmtsRest.

Stmt -> DisjExpr AssignEps ; .
Stmt -> if DisjExpr then Stmts ElseEps end ; .
Stmt -> while DisjExpr do Stmts end ; .

StmtsRest -> .
StmtsRest -> Stmts.

AssignEps -> .
AssignEps -> = DisjExpr .

ElseEps -> .
ElseEps -> else Stmts .